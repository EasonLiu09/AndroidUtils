plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.jfrog.bintray'
    id 'maven-publish'
}


def libVersion = '1.0.0'

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/mqtt-release.aar")
            groupId 'com.eason.utils'
            artifactId 'mqtt'
            version libVersion

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if (it.name.contains("mqtt")) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    // Get Bintray credential from environment variable
    user = BINTRAY_USER // Get bintray User
    key = BINTRAY_API_KEY // Get bintray Secret Key
//    configurations = ['archives']
    pkg {
        repo = 'Utils'
        name = 'com.eason.mqtt'
        userOrg = 'easonliu09'
        licenses = ['Apache-2.0']
        desc = 'Simplify Mqtt usage'
        override = true
        websiteUrl = "https://github.com/EasonLiu09/AndroidUtils"
        vcsUrl = "https://github.com/EasonLiu09/AndroidUtils.git"
        publish = true
        version {
            name = libVersion
        }
    }
    publications = ['Production']

}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.0"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'

    implementation ('org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.2')
}

//if (project.rootProject.file('local.properties').exists()) {
//    apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
//    apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
//}